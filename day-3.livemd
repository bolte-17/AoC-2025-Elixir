# Day 3: Lobby

```elixir
Mix.install([
  {:kino_aoc, "~> 0.1.7"},
  {:memoize, "~> 1.4"}
])
```

## Part 1

You descend a short staircase, enter the surprisingly vast lobby, and are quickly cleared by the security checkpoint. When you get to the main elevators, however, you discover that each one has a red light above it: they're all _offline_.

"Sorry about that," an Elf apologizes as she tinkers with a nearby control panel. "Some kind of electrical surge seems to have fried them. I'll try to get them online soon."

You explain your need to get further underground. "Well, you could at least take the escalator down to the printing department, not that you'd get much further than that without the elevators working. That is, you could if the escalator weren't also <span title="Escalator temporarily stairs. Sorry for the convenience.">offline</span>."

"But, don't worry! It's not fried; it just needs power. Maybe you can get it running while I keep working on the elevators."

There are batteries nearby that can supply emergency power to the escalator for just such an occasion. The batteries are each labeled with their [joltage](https://adventofcode.com/2020/day/10) rating, a value from `1` to `9`. You make a note of their joltage ratings (your puzzle input). For example:

```
987654321111111
811111111111119
234234234234278
818181911112111
```

The batteries are arranged into _banks_; each line of digits in your input corresponds to a single bank of batteries. Within each bank, you need to turn on _exactly two_ batteries; the joltage that the bank produces is equal to the number formed by the digits on the batteries you've turned on. For example, if you have a bank like `12345` and you turn on batteries `2` and `4`, the bank would produce `24` jolts. (You cannot rearrange batteries.)

You'll need to find the largest possible joltage each bank can produce. In the above example:

* In `987654321111111`, you can make the largest joltage possible, _`98`_, by turning on the first two batteries.
* In `811111111111119`, you can make the largest joltage possible by turning on the batteries labeled `8` and `9`, producing _`89`_ jolts.
* In `234234234234278`, you can make _`78`_ by turning on the last two batteries (marked `7` and `8`).
* In `818181911112111`, the largest joltage you can produce is _`92`_.

The total output joltage is the sum of the maximum joltage from each bank, so in this example, the total output joltage is `98` + `89` + `78` + `92` = _`357`_.

There are many batteries in front of you. Find the maximum joltage possible from each bank; _what is the total output joltage?_

```elixir
string_to_digits = fn s -> String.to_charlist(s) |> Enum.map(& &1 - ?0) end

example_joltages = 
  """
    987654321111111
    811111111111119
    234234234234278
    818181911112111
  """
  |> String.split(~r/\s/, trim: true)
  |> Enum.map(string_to_digits)

largest_joltage = fn digits -> 
    digits
    |> Enum.reduce([], fn
          n, [] -> [{n, 0}]
          n, [{a, _} | _] when n > a -> [{n, 0}, {a, n}]
          n, [{a, b} | _] -> [{a, max(b, n)}]
        end)
    |> Enum.find(&elem(&1, 1) > 0)
  |> then(fn {a, b} -> 10 * a + b end)
  end
    
example_joltages
|> Enum.map(&largest_joltage.(&1))
|> IO.inspect(charlists: :as_lists)
|> Enum.sum()
```

<!-- livebook:{"attrs":"eyJhc3NpZ25fdG8iOiJwdXp6bGVfaW5wdXQiLCJkYXkiOiIzIiwic2Vzc2lvbl9zZWNyZXQiOiJBT0MyMDI1X1NFU1NJT04iLCJ5ZWFyIjoiMjAyNSJ9","chunks":null,"kind":"Elixir.KinoAOC.HelperCell","livebook_object":"smart_cell"} -->

```elixir
{:ok, puzzle_input} =
  KinoAOC.download_puzzle("2025", "3", System.fetch_env!("LB_AOC2025_SESSION"))
```

```elixir
puzzle_joltages =
  puzzle_input
  |> String.split()
  |> Enum.map(string_to_digits)

puzzle_joltages
|> Enum.map(&largest_joltage.(&1))
|> Enum.sum()
```

## Part 2

The escalator doesn't move. The Elf explains that it probably needs more joltage to overcome the [static friction](https://en.wikipedia.org/wiki/Static_friction) of the system and hits the big red "joltage limit safety override" button. You lose count of the number of times she needs to confirm "yes, I'm sure" and decorate the lobby a bit while you wait.

Now, you need to make the largest joltage by turning on _exactly twelve_ batteries within each bank.

The joltage output for the bank is still the number formed by the digits of the batteries you've turned on; the only difference is that now there will be _`12`_ digits in each bank's joltage output instead of two.

Consider again the example from before:

```
987654321111111
811111111111119
234234234234278
818181911112111
```

Now, the joltages are much larger:

* In `987654321111111`, the largest joltage can be found by turning on everything except some `1`s at the end to produce _`987654321111`_.
* In the digit sequence `811111111111119`, the largest joltage can be found by turning on everything except some `1`s, producing _`811111111119`_.
* In `234234234234278`, the largest joltage can be found by turning on everything except a `2` battery, a `3` battery, and another `2` battery near the start to produce _`434234234278`_.
* In `818181911112111`, the joltage _`888911112111`_ is produced by turning on everything except some `1`s near the front.

The total output joltage is now much larger: `987654321111` + `811111111119` + `434234234278` + `888911112111` = _`3121910778619`_.

_What is the new total output joltage?_

```elixir
defmodule Recursive do
  use Memoize

  defmemo largest_joltage(_, 0), do: 0
  defmemo largest_joltage(batteries, limit) when length(batteries) < limit, do: 0
  defmemo largest_joltage([d | rest], limit) do
    max(
      largest_joltage(rest, limit),
      d * 10 ** (limit - 1) + largest_joltage(rest, limit - 1)
    )
  end
end

Memoize.invalidate()
example_joltages
|> Enum.map(&Recursive.largest_joltage(&1, 12))
|> IO.inspect()
|> Enum.sum()
```

```elixir
Memoize.invalidate()
puzzle_joltages
|> Enum.map(&Recursive.largest_joltage(&1, 12))
|> Enum.sum()
```

```elixir
defmodule Iterative do
  def largest_joltage(batteries, limit) do
    {to_add, tail} = Enum.split(batteries, -limit)
    for b <- Enum.reverse(to_add), reduce: tail do
      acc when b >= hd(acc) ->
        [b | 
          acc 
          |> Enum.chunk_every(2, 1)
          |> Enum.find_index(fn [a,b] -> b > a; [_] -> true end)
          |> then(&List.delete_at(acc, &1))
        ]
      acc -> acc
    end
    |> Enum.reduce(fn d, a -> a * 10 + d end)
  end
end

example_joltages
|> Enum.map(&Iterative.largest_joltage(&1, 12))
|> IO.inspect()
|> Enum.sum()
```

```elixir
puzzle_joltages
|> Enum.map(&Iterative.largest_joltage(&1, 12))
|> Enum.sum()
```

<!-- livebook:{"offset":6663,"stamp":{"token":"XCP.9ju2UGYtuAIjqdUx3XWEz235k1h7aEvTUfNZK1AD5aj0b95Txn4fSzrk8s48RK3gcUPNRLAuzgmq4ZH1sMpPaEQq2LRyeFvdLWkv0dWLb1hNBVXWjrN0Dub_","version":2}} -->
